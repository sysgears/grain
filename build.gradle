import java.security.MessageDigest

apply plugin: 'maven'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'signing'

buildscript {
    repositories { mavenCentral() }
    dependencies { classpath 'org.eclipse.jgit:org.eclipse.jgit:3.1.0.201310021548-r' }
}

ext {
    applicationProperties = new Properties()
    applicationProperties.load(new FileInputStream("$project.projectDir/src/main/resources/application.properties"))

    grainVersion = applicationProperties.getProperty('grain.version')
    if (!grainVersion) { throw new RuntimeException('Grain version is not set in the properties file') }

    title = 'Grain Static Website Building Framework'
    url = 'http://www.sysgears.com/grain'
    licenseName = 'Apache License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    localGHPagesRepoDir = '../grain.gh-pages'
    localGHPagesRepoCanonicalPath = new File(localGHPagesRepoDir).canonicalPath
    javaCompatibilityVersion = 1.7
    mainClassName = 'com.sysgears.grain.Main'
}

group = 'com.sysgears.grain'
version = project.grainVersion
sourceCompatibility = project.javaCompatibilityVersion
targetCompatibility = project.javaCompatibilityVersion

defaultTasks 'build'

configurations.all {
    exclude group: 'commons-logging'
    exclude group: 'rhino'
}

configurations {
    deployerJars
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.eclipse.jetty:jetty-server:9.2.21.v20170120'
    compile 'org.eclipse.jetty:jetty-servlet:9.2.21.v20170120'
    compile 'javax.servlet:javax.servlet-api:3.1.0'

    compile 'org.yaml:snakeyaml:1.18'

    compile 'org.codehaus.gpars:gpars:1.2.1'

    compile 'com.github.rjeschke:txtmark:0.9'

    compile 'org.scijava:jython-shaded:2.7.0'
    compile 'org.jruby:jruby:9.1.8.0'

    compile 'com.googlecode.htmlcompressor:htmlcompressor:1.5.2'
    compile 'com.yahoo.platform.yui:yuicompressor:2.4.8'

    compile 'commons-io:commons-io:2.5'
    compile 'commons-cli:commons-cli:1.4'

    compile('ch.qos.logback:logback-classic:1.2.3') {
        exclude group: 'org.slf4j'
    }
    compile 'org.slf4j:jcl-over-slf4j:1.7.25'

    compile 'com.google.code.findbugs:jsr305:3.0.1'
    compile 'com.google.inject:guice:4.1.0'
    compile 'com.google.inject.extensions:guice-assistedinject:4.1.0'

    compile 'org.pegdown:pegdown:1.4.2'
    compile 'com.vladsch.flexmark:flexmark-all:0.19.4'

    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testCompile('org.spockframework:spock-guice:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }

    compile 'org.codehaus.groovy:groovy-ant:2.4.12'
    compile 'org.codehaus.groovy:groovy:2.4.12'
    compile('org.codehaus.groovy.modules.http-builder:http-builder:0.5.2') {
        exclude module: 'groovy'
    }

    deployerJars 'org.apache.maven.wagon:wagon-ssh:2.2'
}

sourceSets {
    main {
        groovy {
            srcDirs = ['src/main/groovy']
        }
    }
}

signing {
    sign configurations.archives
}

task getToolsMD5Checksum {
    def md5 = MessageDigest.getInstance('MD5')
    file('tools').eachFileRecurse { file ->
        if (file.isFile()) {
            md5.update(file.bytes)
        }
    }
    def messageDigest = md5.digest()
    def sb = new StringBuilder()
    messageDigest.each {
        sb.append(String.format('%02x', it))
    }
    project.ext.toolsMD5 = sb.toString()
}

task createSharedManifest(dependsOn: getToolsMD5Checksum) {
    project.ext.sharedManifest = manifest {
        attributes 'Implementation-Title': project.title,
                'Implementation-Version': version,
                'Built-By': System.getProperty('user.name'),
                'Built-Rev': project.toolsMD5,
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Main-Class': project.mainClassName
    }
}

jar {
    manifest = project.manifest {
        from sharedManifest
    }
    from(project.rootProject.projectDir) {
        include 'tools/**'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from(sourceSets.main.allSource) {
        exclude 'logback.xml'
    }
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from groovydoc.destinationDir
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    from groovydoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives groovydocJar
    archives jar
}

ext.pomConfiguration = {
    name project.title
    description '''Grain is a static web site building framework for Groovy that makes demanding static web site
            implementation an intuitive and enjoyable task to do.'''
    url project.url
    inceptionYear '2013'

    scm {
        url 'https://github.com/sysgears/grain'
        connection 'scm:https://github.com/sysgears/grain.git'
        developerConnection 'scm:git@github.com:sysgears/grain.git'
    }

    licenses {
        license {
            name project.licenseName
            url project.licenseUrl
            distribution 'repo'
        }
    }

    developers {
        developer {
            id 'sysgears'
            name 'SysGears LLC'
            email 'info@sysgears.com'
        }
    }
}

uploadArchives {
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
        configuration = configurations.deployerJars
        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
            authentication(userName: System.getProperty('sonatypeUsername'), password: System.getProperty('sonatypePassword'))
        }
        snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
            authentication(userName: System.getProperty('sonatypeUsername'), password: System.getProperty('sonatypePassword'))
        }
        pom.project(pomConfiguration)
    }
}

idea {
    module {
        excludeDirs = ['.idea', '.gradle', 'build'].collect { file(it) }
    }
}
